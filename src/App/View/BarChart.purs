module App.View.BarChart
   ( Bar(..)
   , BarChart(..)
   , StackedBar(..)
   ) where

import Prelude hiding (absurd)

import App.Util (class Reflect, Dimensions, SelState, Selectable, 𝕊(..), colorShade, from, getPersistent, getTransient, get_intOrNumber, record)
import App.Util.Selector (ViewSelSetter, barChart, barSegment)
import App.View.Util (class Drawable, class Drawable2, Renderer, selListener, uiHelpers)
import App.View.Util.D3 (createSVG)
import App.View.Util.D3 as D3
import Bind ((↦))
import Data.Int (floor, pow, toNumber)
import Data.Number (log)
import Data.Tuple (fst, snd)
import DataType (f_bars, f_caption, f_size, f_stackedBars, f_x, f_y, f_z)
import Dict (Dict)
import Effect (Effect)
import Foreign.Object (Object, fromFoldable)
import Primitive (string, unpack)
import Util ((!))
import Util.Map (get)
import Val (Val)
import Web.Event.EventTarget (EventListener)

newtype BarChart = BarChart
   { size :: Dimensions (Selectable Int)
   , caption :: Selectable String
   , stackedBars :: Array StackedBar
   }

newtype StackedBar = StackedBar
   { x :: Selectable String
   , bars :: Array Bar
   }

newtype Bar = Bar
   { y :: Selectable String
   , z :: Selectable Number
   }

type BarChartHelpers =
   { bar_attrs :: (Int -> String) -> BarChart -> BarSegmentCoordinate -> Object String
   , tickEvery :: Int -> Int
   }

foreign import drawBarChart :: BarChartHelpers -> Renderer BarChart

barChartHelpers :: BarChartHelpers
barChartHelpers =
   { bar_attrs
   , tickEvery
   }
   where
   bar_attrs :: (Int -> String) -> BarChart -> BarSegmentCoordinate -> Object String
   bar_attrs indexCol (BarChart { stackedBars }) { i, j } =
      fromFoldable
         [ "fill" ↦ case persistent of
              None -> col
              Secondary -> "url(#diagonalHatch-" <> show j <> ")"
              Primary -> colorShade col (-40)
         , "stroke-width" ↦ "1"
         , "stroke-dasharray" ↦ case transient of
              None -> "none"
              Secondary -> "0.5 1" -- "1 2"
              Primary -> "0.5 1" -- "2 2"
         , "stroke-linecap" ↦ "round"
         , "stroke" ↦
              if persistent /= None || transient /= None then colorShade col (-70)
              else col
         ]
      where
      StackedBar { bars } = stackedBars ! i
      Bar { z } = bars ! j
      t = snd z
      persistent = getPersistent t
      transient = getTransient t
      col = indexCol j

   tickEvery :: Int -> Int
   tickEvery n =
      if n <= 2 * pow 10 m then 2 * pow 10 (m - 1)
      else pow 10 m
      where
      m = floor (log (toNumber n) / log 10.0)

setSelState2 :: BarChart -> EventListener -> D3.Selection -> Effect Unit
setSelState2 _ _ _ =
   pure unit

createRootElement2 :: BarChart -> D3.Selection -> String -> Effect D3.Selection
createRootElement2 (BarChart { size }) div childId = do
   rootElement <- div # createSVG (size <#> fst) childId
   pure rootElement

instance Drawable2 BarChart where
   createRootElement = createRootElement2
   setSelState = setSelState2

instance Drawable BarChart where
   draw rSpec figVal _ redraw =
      drawBarChart barChartHelpers uiHelpers rSpec =<< selListener figVal redraw barSegment'
      where
      barSegment' :: ViewSelSetter BarSegmentCoordinate
      barSegment' { i, j } = barSegment i j >>> barChart

instance Reflect (Dict (Val (SelState 𝕊))) BarChart where
   from r = BarChart
      { size: record from (get f_size r)
      , caption: unpack string (get f_caption r)
      , stackedBars: record from <$> from (get f_stackedBars r)
      }

instance Reflect (Dict (Val (SelState 𝕊))) StackedBar where
   from r = StackedBar
      { x: unpack string (get f_x r)
      , bars: record from <$> from (get f_bars r)
      }

instance Reflect (Dict (Val (SelState 𝕊))) Bar where
   from r = Bar
      { y: unpack string (get f_y r)
      , z: get_intOrNumber f_z r
      }

-- see data binding in .js
type BarSegmentCoordinate = { i :: Int, j :: Int }
