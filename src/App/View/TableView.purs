module App.View.TableView where

import Prelude

import App.Util (SelState, ð•Š(..), eventData, isTransient, selClassesFor, selected)
import App.Util.Selector (ViewSelSetter, field, listElement)
import App.View.Util (class Drawable, Renderer, selListener, uiHelpers)
import Data.Array (filter, head, null, sort)
import Data.Maybe (fromJust)
import Data.Set (toUnfoldable)
import Dict (Dict)
import Effect (Effect)
import Partial.Unsafe (unsafePartial)
import Util (Endo, (!))
import Util.Map (keys, lookup)
import Val (BaseVal, Val(..), Array2)
import Web.Event.EventTarget (EventListener, eventListener)

newtype TableView = TableView
   { title :: String
   , filter :: Boolean
   -- field names of homogeneous array of records with fields of primitive type
   , colNames :: Array String
   -- values of the records. Each row has the same length as colNames
   , table :: Array2 (Val (SelState ð•Š)) -- somewhat anomalous, as elsewhere we have Selectables
   }

-- helper functions used by View.purs to decompose array of records (Dict (Val (SelState ð•Š))) into colNames and table
headers :: Array (Dict (Val (SelState ð•Š))) -> Array String
headers records = sort <<< toUnfoldable <<< keys <<< unsafePartial fromJust $ head records

arrayDictToArray2 :: forall a. Array String -> Array (Dict a) -> Array2 a
arrayDictToArray2 colNames = map (dictToArray colNames)
   where
   dictToArray keys d = map (\k -> unsafePartial fromJust $ lookup k d) keys

isCellTransient :: Array2 (Val (SelState ð•Š)) -> Int -> Int -> Boolean
isCellTransient table i j = isTransient <<< tableViewHelpers.val_selState $ table ! i ! j

foreign import drawTable :: TableViewHelpers -> EventListener -> Renderer TableView

type TableViewHelpers =
   { rowKey :: String
   , record_isUsed :: Array (Val (SelState ð•Š)) -> Boolean
   , cell_selClassesFor :: String -> SelState ð•Š -> String
   -- values in table cells are not "unpacked" to Selectable but remain as Val
   , val_val :: Val (SelState ð•Š) -> BaseVal (SelState ð•Š)
   , val_selState :: Val (SelState ð•Š) -> SelState ð•Š
   }

tableViewHelpers :: TableViewHelpers
tableViewHelpers =
   { rowKey
   , record_isUsed
   , cell_selClassesFor
   , val_val: \(Val _ v) -> v
   , val_selState: \(Val Î± _) -> Î±
   }
   where
   rowKey :: String
   rowKey = "__n"

   record_isUsed :: Array (Val (SelState ð•Š)) -> Boolean
   record_isUsed r = not <<< null $ flip filter r \v -> selected $ (_ /= None) <$> (v # \(Val Î± _) -> Î±)

   cell_selClassesFor :: String -> SelState ð•Š -> String
   cell_selClassesFor colName s
      | colName == rowKey = ""
      | otherwise = selClassesFor s

instance Drawable TableView where
   draw rSpec figVal _ redraw = do
      toggleListener <- filterToggleListener filterToggler
      drawTable tableViewHelpers toggleListener uiHelpers rSpec
         =<< selListener figVal redraw tableViewSelSetter
      where
      tableViewSelSetter :: ViewSelSetter CellIndex
      tableViewSelSetter { i, colName } = listElement i <<< field colName

      filterToggleListener :: FilterToggler -> Effect EventListener
      filterToggleListener toggler = eventListener (eventData >>> toggler >>> (\_ -> identity) >>> redraw)

-- convert mouse event data (here, always rowKey) to view change
type FilterToggler = String -> Endo TableView

filterToggler :: FilterToggler
filterToggler _ (TableView view) = TableView view { filter = not view.filter }

-- 0-based index of selected record and name of field; see data binding in .js (-1th field name is __n, the rowKey)
type CellIndex = { i :: Int, colName :: String }
