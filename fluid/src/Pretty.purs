module Pretty (PrettyShow(..), class Pretty, compare, pretty, prettyP) where

import Prelude

import Bind (Bind, Var, (↦))
import Data.List (List(..), fromFoldable, singleton, (:))
import Data.List.NonEmpty (NonEmptyList(..), head, toList)
import Data.Maybe (Maybe(..))
import Data.Newtype (class Newtype)
import Data.NonEmpty ((:|))
import Data.Traversable (class Foldable)
import DataType (Ctr, cCons)
import Dict (Dict)
import Expr (Cont(..), Elim(..))
import Expr as E
import Lattice (class BotOf, class MeetSemilattice, class Neg, botOf, symmetricDiff)
import Pretty.Doc (Doc, empty, expr, indent, inlOrMul, line, render, stmt, stmtOrExpr, text, (<++>), (<+>), (</>))
import Pretty.Util (assignment, block, braces, brackets, hsep, matrix, number, pair, parens, record, sep', string, vsep)
import Primitive.Parse (getPrec)
import SExpr (Branch, Clause(..), Clauses(..), DictEntry(..), Expr(..), ListRest(..), ListRestPattern(..), ParagraphElem(..), Pattern(..), Qualifier(..), RecDefs, Stmt(..), VarDef(..), VarDefs)
import Util (type (×), error, isEmpty, (×))
import Util.Map (toUnfoldable)
import Util.Pair (Pair(..))
import Val (BaseVal(..), Fun(..)) as V
import Val (class Ann, class Highlightable, BaseVal, DictRep(..), Env(..), EnvStmt(..), ForeignOp(..), Fun, MatrixRep(..), Val(..), highlightIf)

class Pretty p where
   pretty :: p -> Doc

newtype PrettyShow a = PrettyShow a

derive instance Newtype (PrettyShow a) _

instance Pretty a => Show (PrettyShow a) where
   show (PrettyShow x) = pretty x # render

instance Pretty String where
   pretty = text

class RootOp (e :: Type) where
   rootOp :: e -> Maybe String

instance RootOp Pattern where
   rootOp (PConstr c _) | c == cCons = Just ":"
   rootOp _ = Nothing

instance Ann a => RootOp (Expr a) where
   rootOp (Constr _ c _) | c == cCons = Just ":"
   rootOp (BinaryApp _ op _) = Just op
   rootOp (UnaryPrefixApp op _) = Just op
   rootOp _ = Nothing

instance Highlightable a => RootOp (E.Expr a) where
   rootOp (E.Constr _ c _) | c == cCons = Just ":"
   rootOp _ = Nothing

instance Highlightable a => RootOp (Val a) where
   rootOp (Val _ Nothing u) = rootOp u
   rootOp (Val _ (Just _) _) = Nothing

instance Highlightable a => RootOp (BaseVal a) where
   rootOp (V.Constr c _) | c == cCons = Just ":"
   rootOp _ = Nothing

class IsSimple (e :: Type) where
   isSimple :: e -> Boolean

instance Ann a => IsSimple (Expr a) where
   isSimple (BinaryApp _ _ _) = false
   isSimple (UnaryPrefixApp _ _) = false
   isSimple (Constr _ c _) | c == cCons = false
   isSimple (Lambda _) = false
   isSimple (Ternary _ _ _) = false
   isSimple _ = true

instance Highlightable a => IsSimple (E.Expr a) where
   isSimple (E.Constr _ c _) | c == cCons = false
   isSimple (E.Lambda _ _) = false
   isSimple (E.Let _ _) = false
   isSimple (E.LetRec _ _) = false
   isSimple _ = true

instance Highlightable a => IsSimple (Val a) where
   isSimple (Val _ Nothing u) = isSimple u
   isSimple (Val _ (Just _) _) = false

instance Highlightable a => IsSimple (BaseVal a) where
   isSimple _ = true

instance IsSimple Pattern where
   isSimple _ = true

prettySimple :: forall a. IsSimple a => Pretty a => a -> Doc
prettySimple s =
   if isSimple s then pretty s
   else parens (pretty s)

prettyP :: forall a. Pretty a => a -> String
prettyP x = render (pretty x)

operatorApp :: forall a. Ann a => Int -> Expr a -> Doc
operatorApp n (BinaryApp s op s') =
   case getPrec op of
      -1 -> operatorApp customPrec s <+> text "|" <> text op <> text "|" <+> operatorApp customPrec s'
         where
         customPrec = getPrec "|x|"
      n' ->
         if n' <= n then
            parens (operatorApp n' s <+> text op <+> operatorApp n' s')
         else
            operatorApp n' s <+> text op <+> operatorApp n' s'
operatorApp n (UnaryPrefixApp op s) =
   case getPrec op of
      -1 -> error "not implemented!"
      n' ->
         if n' <= n then
            parens (text op <+> operatorApp n' s)
         else
            text op <+> operatorApp n' s
operatorApp _ e = prettySimple e

lambda :: forall a. Ann a => List Pattern -> Stmt a -> Doc
lambda ps s = text "lambda" <+> prettyList ps <> text ":" <+> prettyLambdaBody s
   where
   prettyLambdaBody :: Stmt a -> Doc
   prettyLambdaBody (Return e) = pretty e
   prettyLambdaBody s' = pretty s'

instance Ann a => Pretty (Expr a) where
   pretty (Var x) = text x
   pretty (Op o) = parens $ text o
   pretty (Int α n) = highlightIf α (number n)
   pretty (Float α n) = highlightIf α (number n)
   pretty (Str α str) = highlightIf α (string str)
   pretty (Constr α c Nil) = highlightIf α (text c)
   pretty (Constr α c as) = highlightIf α (expr $ prettyConstr c as)
   pretty (Dictionary α Nil) = highlightIf α (text "{}")
   pretty (Dictionary α es) = highlightIf α (expr $ record $ map pretty es)
   pretty (Matrix α e (x × y) e') =
      highlightIf α (expr $ matrix (pretty e <+> text "for" <+> pair text x y <+> text "in" <+> pretty e'))
   pretty (Lambda cs) = pretty cs -- Clauses
   pretty (Project s x) = expr $ prettySimple s <> text "." <> text x
   pretty (DProject e k) = expr $ prettySimple e <> brackets (expr $ pretty k)
   pretty (App s s') = expr $ prettyAppChain (App s s') Nil
   pretty (BinaryApp s op s') = expr $ operatorApp 0 (BinaryApp s op s')
   pretty (UnaryPrefixApp op s) = expr $ operatorApp 0 (UnaryPrefixApp op s)
   pretty (Ternary cond e1 e2) =
      expr $ pretty e1 <+> text "if" <+> pretty cond <+> text "else" <+> pretty e2

   pretty (ListEmpty α) = highlightIf α (text "[]")
   pretty (ListNonEmpty α e rest) =
      highlightIf α (text "[")
         <> inlOrMul
            (pretty e <> collect rest true)
            (indent (line <> pretty e) <> collect rest false)
      where
      collect :: ListRest a -> Boolean -> Doc
      collect (Next α' e' rest') inline = highlightIf α' (text ",") <> (if inline then text " " <> pretty e' else indent (line <> pretty e')) <> collect rest' inline
      collect (End α') inline = if inline then highlightIf α' (text "]") else line <> highlightIf α' (text "]")

   pretty (ListEnum s s') = brackets $ expr (pretty s <+> text ".." <+> pretty s')
   pretty (ListComp α s qs) = highlightIf α (brackets (expr (pretty s) <+> pretty qs)) -- Qualifier
   pretty (Paragraph p) = pretty p
   pretty (DocExpr p e) = text "@doc" <> parens (pretty p) </> pretty e

instance Ann a => Pretty (List (Qualifier a)) where
   pretty (Cons (ListCompDecl (VarDef v s)) Nil) =
      text "def" <+> pretty v <> text ":" <+> pretty s
   pretty (Cons (ListCompGuard s) Nil) = text "if" <+> pretty s
   pretty (Cons (ListCompGen p s) Nil) = text "for" <+> pretty p <+> text "in" <+> pretty s
   pretty (Cons q qs) = pretty (singleton q) <+> pretty qs
   pretty Nil = empty

instance Ann a => Pretty (NonEmptyList (Pattern × Stmt a)) where
   pretty cs = vsep (toList (pretty <$> cs))

instance Ann a => Pretty (Pattern × Stmt a) where
   pretty (p × b) = text "case" <+> (pretty p) <> block (pretty b)

instance Pretty Pattern where
   pretty (PVar x) = text x
   pretty (PRecord xps) = record $ map pretty xps
   pretty (PConstr c Nil) = text c
   pretty (PConstr c ps) = prettyConstr c ps
   pretty (PListEmpty) = text "[]"
   pretty (PListNonEmpty p l) = brackets (pretty p <> pretty l)

instance Pretty (String × Pattern) where
   pretty (k × v) = text k <> text ":" <+> pretty v

instance Pretty ListRestPattern where
   pretty (PListVar x) = text x
   pretty (PListNext p l) = text "," <+> pretty p <> pretty l
   pretty PListEnd = empty

instance Ann a => Pretty (VarDef a) where
   pretty (VarDef v s) = text "def" <+> pretty v <+> assignment (pretty s)

instance Ann a => Pretty (VarDefs a) where
   pretty ds = sep' (stmtOrExpr line (text " ")) (toList (pretty <$> ds))

instance Ann a => Pretty (Stmt a) where
   pretty (Return e) = text "return" <+> pretty e
   pretty (If (NonEmptyList (ss :| sss)) e) =
      vsep (prettyClause "if" ss : (prettyClause "elif" <$> sss))
         <++> text "else" <> block (pretty e)
      where
      prettyClause w (s × b) = text w <+> expr (pretty s) <> block (pretty b)
   pretty (Match s cs) = text "match" <+> pretty s <> block (pretty cs)
   pretty (Def ds body) = pretty ds <> line <> pretty body
   pretty (DefRec xcs body) = pretty xcs <> line <> pretty body

instance Ann a => Pretty (Clause a) where
   pretty (Clause (ps × b)) = lambda (toList ps) b

instance Ann a => Pretty (Clauses a) where
   pretty (Clauses cs) = pretty (head cs) -- TODO: head ?

instance Ann a => Pretty (RecDefs a) where
   pretty bs = sep' (stmtOrExpr line (text " ")) (toList (pretty <$> bs))

instance Ann a => Pretty (Branch a) where
   pretty (v × Clause (ps × b)) =
      text "def"
         <+> text v
         <> parens (prettyList (toList ps))
         <> block (pretty b)

instance Ann a => Pretty (DictEntry a × Expr a) where
   pretty (k × v) =
      pretty k <> stmt
         ( inlOrMul
              (text ":" <+> pretty v)
              (text ":" <> indent (line <> pretty v))
         )

instance Ann a => Pretty (DictEntry a) where
   pretty (ExprKey k) = brackets (pretty k)
   pretty (VarKey a k) = highlightIf a (text k)

instance Ann a => Pretty (List (ParagraphElem a)) where
   pretty xs = text "f\"\"\"" <> hsep (pretty <$> xs) <> text "\"\"\""

instance Ann a => Pretty (ParagraphElem a) where
   pretty (Token str) = text str
   pretty (Unquote e) = text "{" <> pretty e <> text "}"

prettyConstr :: forall a. RootOp a => IsSimple a => Pretty a => Ctr -> List a -> Doc
prettyConstr "Nil" Nil = text "[]"
prettyConstr "Pair" (x : y : Nil) = pair pretty x y
prettyConstr ":" (x : y : Nil) = prettyConsArg x true <+> text ":|" <+> prettyConsArg y false
prettyConstr c Nil = text c
prettyConstr c ps = text c <> parens (prettyList ps)

prettyConsArg :: forall a. RootOp a => IsSimple a => Pretty a => a -> Boolean -> Doc
prettyConsArg e lhs = case rootOp e of
   Nothing -> prettySimple e
   Just op -> if (if lhs then (<=) else (<)) (getPrec op) (getPrec ":") then parens (pretty e) else pretty e

prettyAppChain :: forall a. Ann a => Expr a -> List (Expr a) -> Doc
prettyAppChain (App f a) as = prettyAppChain f (a : as)
prettyAppChain f as = prettySimple f <> parens (prettyList as)

commas :: List Doc -> Doc
commas Nil = empty
commas (d : Nil) = d
commas (d : ds) = d <> text "," <+> commas ds

vcommas :: List Doc -> Doc
vcommas Nil = empty
vcommas (d : Nil) = d
vcommas (d : ds) = d <> text "," <++> vcommas ds

prettyList :: forall f a. Foldable f => Pretty a => f a -> Doc
prettyList xs = commas (pretty <$> fromFoldable xs)

instance Highlightable a => Pretty (Pair (E.Expr a)) where
   pretty (Pair k v) = pretty k <> text ":" <+> pretty v

instance Highlightable a => Pretty (E.Expr a) where
   pretty (E.Var x) = text x
   pretty (E.Op op) = parens (text op)
   pretty (E.Int a n) = highlightIf a (number n)
   pretty (E.Float a n) = highlightIf a (number n)
   pretty (E.Str a str) = highlightIf a (string str)
   pretty (E.Dictionary a ees) = highlightIf a $ record (pretty <$> ees)
   pretty (E.Constr a c es) = highlightIf a (prettyConstr c es)
   pretty (E.Matrix a e1 (i × j) e2) =
      highlightIf a $ matrix (pretty e1 <+> text "for" <+> pair text i j <+> text "in" <+> pretty e2)
   pretty (E.Lambda a o) = highlightIf a (text "lambda") <+> pretty o -- really?
   pretty (E.DProject e x) = pretty e <> brackets (pretty x)
   pretty (E.App e e') = pretty e <> parens (pretty e') -- TODO
   pretty (E.Let (E.VarDef o e) e') = text "def" <+> pretty o <> block (pretty e) <++> pretty e'
   pretty (E.LetRec (E.RecDefs _ p) e') = text "def" <+> pretty p <++> pretty e'
   pretty (E.DocExpr p e) = text "@doc" <> parens (pretty p) <+> pretty e

instance Highlightable a => Pretty (E.Stmt a) where
   pretty (E.Return e) = text "return" <+> pretty e
   pretty (E.Match e σ) = text "match" <+> pretty e <> block (pretty σ)
   pretty (E.Def (E.VarDef o e) s) = text "def" <+> pretty o <> block (pretty e) <++> pretty s
   pretty (E.DefRec (E.RecDefs _ ρ) s) = text "def" <+> pretty ρ <++> pretty s

instance Highlightable a => Pretty (Cont a) where
   pretty (ContExpr e) = pretty e
   pretty (ContElim σ) = pretty σ

instance Highlightable a => Pretty (Elim a) where
   pretty (ElimVar x k) = pretty x <> text "->" <> pretty k
   pretty (ElimConstr ks) = prettyList ks
   pretty (ElimDict xs k) = braces (prettyList xs) <+> text "->" <+> braces (pretty k)

instance Highlightable a => Pretty (Dict (Elim a)) where
   pretty ρ = go (toUnfoldable ρ)
      where
      go :: List (Var × Elim a) -> Doc
      go Nil = empty
      go (xσ : Nil) = pretty xσ
      go (xσ : δ) = (go δ <+> text ";") <+> (pretty xσ)

instance Highlightable a => Pretty (Env a) where
   pretty (Env γ) = brackets $ go (toUnfoldable γ)
      where
      go :: List (Var × Val a) -> Doc
      go Nil = empty
      go ((x × v) : rest) =
         (text x <+> text "->" <+> pretty v <+> text ",") <++> go rest

instance Highlightable a => Pretty (EnvStmt a) where
   pretty (EnvStmt γ s) = (pretty γ) <++> (pretty s)

instance Highlightable a => Pretty (Bind (Elim a)) where
   pretty (x ↦ σ) = pretty x <> pretty ":" <+> pretty σ

instance Highlightable a => Pretty (Val a) where
   pretty (Val a Nothing u) = highlightIf a (pretty u)
   pretty (Val a (Just v') u) = text "@doc" <> parens (pretty v') <+> highlightIf a (pretty u)

instance Highlightable a => Pretty (Var × (a × Val a)) where
   pretty (k × (a × v)) = highlightIf a (pretty k) <> text ":" <+> pretty v -- ???

instance Highlightable a => Pretty (BaseVal a) where
   pretty (V.Int n) = number n
   pretty (V.Float n) = number n
   pretty (V.Str str) = string str
   pretty (V.Dictionary (DictRep svs))
      | isEmpty svs = text "{}"
      | otherwise = record (pretty <$> (toUnfoldable svs))
   pretty (V.Constr c vs) = prettyConstr c vs
   pretty (V.Matrix (MatrixRep (vss × _ × _))) = vcommas $ fromFoldable (prettyList <$> vss) -- ???
   pretty (V.Fun phi) = pretty phi

instance Highlightable a => Pretty (Fun a) where
   pretty (V.Closure _ _ _) = text "cl"
   pretty (V.Foreign phi _) = pretty phi
   pretty (V.PartialConstr c vs) = prettyConstr c vs

instance Pretty ForeignOp where
   pretty (ForeignOp (s × _)) = pretty s

compare :: forall a. BotOf a a => Neg a => MeetSemilattice a => Eq a => Pretty a => String -> String -> a -> a -> String × String
compare op1 op2 x y =
   let
      x_minus_y × y_minus_x = symmetricDiff x y
      left = if x_minus_y == botOf x then "" else op1 <> " but not " <> op2 <> ":\n" <> prettyP x_minus_y
      right = if y_minus_x == botOf x then "" else op2 <> " but not " <> op1 <> ":\n" <> prettyP y_minus_x
   in
      left × right
