module Module where

import Prelude

import Control.Monad.Except (class MonadError)
import Control.Monad.Reader (class MonadReader, ask)
import Bind (dottedName, pathName, prefixOf)
import Data.List.NonEmpty (snoc, unsnoc, fromList) as NEL
import Data.Either (Either(..))
import Data.Foldable (foldM, for_, intercalate)
import Data.List (List(..), catMaybes, elem, filter, mapMaybe, reverse, takeWhile, (:))
import Data.Map (Map)
import Data.Map as Map
import Data.Maybe (Maybe(..), isJust)
import Data.Set (Set)
import Data.Set as Set
import Data.Traversable (traverse)
import DataType (class HasClasses, ClassTable, cNoArgs)
import Desugarable (desug)
import Effect.Aff.Class (class MonadAff)
import Effect.Exception (Error)
import Eval (GraphConfig, evalImport, loadPredefined)
import Expr (Import(..)) as E
import Expr (Module, Stmt, fv)
import File (class LoadFile, File(..), FileCxt(..), fluidExtension, loadFile, loadFileMaybe, withClasses)

import Graph (Vertex, vertices)
import Graph.GraphImpl (GraphImpl)
import Graph.WithGraph (AllocT, alloc, runAllocT, runWithGraphT_spy)
import Lattice (Raw)
import ModuleGraph (DependencyGraph, ModuleName, predefined, predefinedDeps)
import Parse (parseModule, parseProgram)
import SExpr (desugarModuleFwd)
import DefiniteAssignment (ClassEntry, Cxt, Entry(..), WfResult(..), erase)
import WellFormed (LoadedModule, checkProgram, mainModule)
import SExpr as S
import Util (type (×), check, orThrow, throwLeft, whenever, withMsg, (×))
import Util.Map (constMap, keys, findWithDefault, maplet, restrict, (<+>))
import Util.Set (empty, (∪))
import Val (class HasModuleStore, moduleStore, modifyModuleStore, val, Env)
import Val (BaseVal(..)) as V

type Config = { s :: Raw S.Stmt, e :: Raw Stmt, gconfig :: GraphConfig }

hasSourceFile :: forall m. MonadAff m => MonadError Error m => MonadReader FileCxt m => LoadFile m => ModuleName -> m Boolean
hasSourceFile q = do
   FileCxt { fluidSrcPaths } <- ask
   isJust <$> loadFileMaybe fluidSrcPaths (File (pathName q <> fluidExtension))

parents :: ModuleName -> List ModuleName
parents q = case NEL.fromList (NEL.unsnoc q).init of
   Nothing -> Nil
   Just q' -> parents q' <> (q' : Nil)

importDeps
   :: forall m
    . MonadAff m
   => MonadError Error m
   => MonadReader FileCxt m
   => LoadFile m
   => ModuleName
   -> S.Import
   -> m { edges :: List ModuleName, load :: List ModuleName }
importDeps enclosing (S.Import q f) = do
   ps <- existing (parents q)
   subs <- case f of
      Nothing -> pure Nil
      Just xs -> existing ((NEL.snoc q) <$> xs)
   let
      prefixEdges = case f of
         Nothing -> filter (_ /= enclosing) (parents q)
         Just _ -> filter (\p -> not (p `prefixOf` enclosing)) (parents q)
   pure { edges: (q : subs) <> prefixEdges, load: ps <> (q : subs) }
   where
   existing = map catMaybes <<< traverse (\m' -> hasSourceFile m' <#> \b -> whenever b m')

checkAcyclic :: DependencyGraph -> List ModuleName -> Either String Unit
checkAcyclic edges roots = void (foldM (go Nil) Set.empty roots)
   where
   go :: List ModuleName -> Set ModuleName -> ModuleName -> Either String (Set ModuleName)
   go path done q
      | Set.member q done = pure done
      | q `elem` path = Left
           ("import cycle: " <> intercalate " -> " (dottedName <$> (q : reverse (takeWhile (_ /= q) path)) <> (q : Nil)))
      | otherwise = Set.insert q <$> foldM (go (q : path)) done (findWithDefault Nil q edges)

noArgsClass :: ClassEntry
noArgsClass = { cxt: Map.empty, name: cNoArgs, base: Nothing, fields: Nil }

classTable :: Map ModuleName Cxt -> ClassTable
classTable modCxt =
   Map.fromFoldable (map (\cls -> dottedName cls.name × cls) (Map.values modCxt >>= classValues))
   where
   classValues cxt = mapMaybe classOf (Map.values cxt)
   classOf = case _ of
      Class cls -> Just cls
      _ -> Nothing

allocTopLevel
   :: forall m
    . HasClasses m
   => HasModuleStore m
   => MonadAff m
   => MonadError Error m
   => MonadReader FileCxt m
   => LoadFile m
   => Raw Env
   -> Map ModuleName (Raw Module)
   -> List S.Import
   -> m (Int × Env Vertex)
allocTopLevel primitives mods imports = do
   n × _ × γ <- flip runAllocT 0 do
      primitives' <- alloc primitives
      mods' <- traverse alloc mods
      let mαs = Set.unions (vertices <$> Map.values mods')
      _ × γ <-
         runWithGraphT_spy
            ( do
                 modifyModuleStore (_ { moduleBody = mods' })
                 γ0 <- foldM (loadPredefined primitives') empty predefined
                 modifyModuleStore (_ { γ0 = γ0 })
                 γ1 <- foldM (\γ (S.Import q f) -> evalImport mainModule γ (E.Import q f)) γ0 imports
                 vName <- val Nothing Set.empty (V.Str "__main__")
                 pure (γ1 <+> maplet "__name__" vName)
            )
            (vertices primitives' ∪ mαs) :: AllocT m (GraphImpl × _)
      pure γ
   pure (n × γ)

prepConfig
   :: forall m
    . HasClasses m
   => HasModuleStore m
   => MonadAff m
   => MonadError Error m
   => MonadReader FileCxt m
   => LoadFile m
   => Raw Env
   -> String
   -> m Config
prepConfig primitives fluidSrc = do
   s × imports <- throwLeft $ parseProgram fluidSrc
   let nativeBuiltins = constMap (VarStatus true) (keys primitives) `Map.union` Map.singleton "__NoArgs" (Class noArgsClass)
   mods <- parseModules imports
   { cxt: cxt_wf, s: s_wf, loaded } <- orThrow (checkProgram mods nativeBuiltins imports s)
   let classes = classTable (_.cxt <$> loaded)
   withClasses classes do
      desugaredMods <- traverse (\m -> (unit <$ _) <$> desugarModuleFwd (Returns <$ m)) (Map.mapMaybe _.mod loaded)
      n × γ <- allocTopLevel primitives desugaredMods imports
      check (Map.keys cxt_wf == Set.fromFoldable (keys γ)) "reduced context matches top-level environment"
      { moduleEnv } <- moduleStore
      for_ (Map.toUnfoldable loaded :: List (ModuleName × LoadedModule)) \(q × { cxt, mod }) ->
         when (isJust mod) $ for_ (Map.lookup q moduleEnv) \γ_q ->
            check (Map.keys (erase cxt) == Set.fromFoldable (keys γ_q))
               ("module " <> dottedName q <> ": members match its environment")
      e_wf <- desug s_wf
      let e = (unit <$ e_wf) :: Raw Stmt
      let gconfig = { n, γ: restrict (fv e) γ, classes }
      pure { s, e, gconfig }

parseModules
   :: forall m
    . MonadAff m
   => MonadError Error m
   => MonadReader FileCxt m
   => LoadFile m
   => List S.Import
   -> m (Map ModuleName (Raw S.Module))
parseModules imports = do
   imported <- traverse (importDeps mainModule) imports
   let roots = predefined <> (imported >>= _.load)
   importGraph × mods <- collectModules Set.empty Map.empty Map.empty roots
   orThrow (checkAcyclic importGraph (imported >>= _.edges))
   -- prefix-closed: a package with no source file of its own is an empty module
   let ancestors = Set.fromFoldable ((Set.toUnfoldable (Map.keys mods) :: List ModuleName) >>= parents)
   pure (mods `Map.union` constMap (S.Module Nil Nil) ancestors)

   where

   collectModules
      :: Set ModuleName
      -> DependencyGraph
      -> Map ModuleName (Raw S.Module)
      -> List ModuleName
      -> m (DependencyGraph × Map ModuleName (Raw S.Module))
   collectModules visited importGraph mods pending = case pending of
      Nil -> pure $ (importGraph × mods)
      mod : rest ->
         if Set.member mod visited then
            collectModules visited importGraph mods rest
         else do
            mod' × edges × toLoad <- parseAndCollect mod
            collectModules
               (Set.insert mod visited)
               (Map.insert mod edges importGraph)
               (Map.insert mod mod' mods)
               (toLoad <> rest)

   parseAndCollect :: ModuleName -> m (Raw S.Module × List ModuleName × List ModuleName)
   parseAndCollect path = do
      FileCxt { fluidSrcPaths } <- ask
      src <- loadFile fluidSrcPaths (File (pathName path <> fluidExtension))
      mod × _ <- throwLeft <#> withMsg ("Loading module " <> dottedName path) $ parseModule src
      imported <- case mod of S.Module is _ -> traverse (importDeps path) is
      let edges = imported >>= _.edges
      let toLoad = predefinedDeps path <> (imported >>= _.load)
      pure $ mod × edges × toLoad
