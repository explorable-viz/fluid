module Parse where

import Prelude hiding (absurd, add, between, join)
import Control.Alt ((<|>))
import Control.Apply (lift2)
import Control.Lazy (fix)
import Control.MonadPlus (empty)
import Data.Array (elem, fromFoldable)
import Data.Either (choose)
import Data.Function (on)
import Data.Identity (Identity)
import Data.List (List(..), (:), concat, foldr, groupBy, singleton, snoc, sortBy)
import Data.List.NonEmpty (NonEmptyList(..), toList)
import Data.Map (values)
import Data.NonEmpty ((:|))
import Data.Ordering (invert)
import Data.Profunctor.Choice ((|||))
import Text.Parsing.Parser.Combinators (try)
import Text.Parsing.Parser.Expr (Operator(..), OperatorTable, buildExprParser)
import Text.Parsing.Parser.Language (emptyDef)
import Text.Parsing.Parser.String (char, eof, oneOf)
import Text.Parsing.Parser.Token (
  GenLanguageDef(..), LanguageDef, TokenParser, alphaNum, letter, makeTokenParser, unGenLanguageDef
)
import DataType (Ctr(..), cPair, isCtrName, isCtrOp)
import Expr (Var)
import Lattice (ğ”¹)
import Primitive (opDefs)
import SExpr (
   Branch, Clause, Expr(..), ListRest(..), ListPatternRest(..), Module(..), Pattern(..), RawExpr(..), RecDefs,
   VarDef, VarDefs, expr
)
import Util (Endo, (Ã—), type (+), error, onlyIf)
import Util.Parse (SParser, sepBy_try, sepBy1, sepBy1_try, some)

-- constants (should also be used by prettyprinter)
strArrow       = "->"      :: String
strAs          = "as"      :: String
strBackslash   = "\\"      :: String
strEquals      = "="       :: String
strFun         = "fun"     :: String
strIn          = "in"      :: String
strLet         = "let"     :: String
strMatch       = "match"   :: String

languageDef :: LanguageDef
languageDef = LanguageDef (unGenLanguageDef emptyDef) {
   commentStart = "{-",
   commentEnd = "-}",
   commentLine = "--",
   nestedComments = true,
   identStart = letter <|> char '_',
   identLetter = alphaNum <|> oneOf ['_', '\''],
   opStart = opChar,
   opLetter = opChar,
   reservedOpNames = [],
   reservedNames = [strAs, strFun, strIn, strLet, strMatch],
   caseSensitive = true
} where
   opChar :: SParser Char
   opChar = oneOf [
      ':', '!', '#', '$', '%', '&', '*', '+', '.', '/', '<', '=', '>', '?', '@', '\\', '^', '|', '-', '~'
   ]

token :: TokenParser
token = makeTokenParser languageDef

-- 'reserved' parser only checks that str isn't a prefix of a valid identifier, not that it's in reservedNames.
keyword âˆ· String â†’ SParser Unit
keyword str =
   if str `elem` (unGenLanguageDef languageDef).reservedNames
   then token.reserved str
   else error $ str <> " is not a reserved word"

ident âˆ· SParser Var
ident = do
   x <- token.identifier
   onlyIf (not $ isCtrName x) x

ctr :: SParser Ctr
ctr = do
   x <- token.identifier
   onlyIf (isCtrName x) $ Ctr x

simplePattern :: Endo (SParser Pattern)
simplePattern pattern' =
   listEmpty <|>
   listNonEmpty <|>
   try constr <|>
   try var <|>
   try (token.parens pattern') <|>
   pair

   where
   listEmpty :: SParser Pattern
   listEmpty = token.brackets $ pure $ PListEmpty

   listNonEmpty :: SParser Pattern
   listNonEmpty =
      token.symbol "[" *> (PListNonEmpty <$> pattern' <*> fix listRest)

         where
         listRest :: Endo (SParser ListPatternRest)
         listRest listRest' =
            token.symbol "]" *> pure PEnd <|>
            token.comma *> (PNext <$> pattern' <*> listRest')

   -- Constructor name as a nullary constructor pattern.
   constr :: SParser Pattern
   constr = PConstr <$> ctr <@> Nil

   -- TODO: anonymous variables
   var :: SParser Pattern
   var = PVar <$> ident

   pair :: SParser Pattern
   pair =
      token.parens $ do
         Ï€ <- pattern' <* token.comma
         Ï€' <- pattern'
         pure $ PConstr cPair (Ï€ : Ï€' : Nil)

arrow :: SParser Unit
arrow = token.reservedOp strArrow

equals :: SParser Unit
equals = token.reservedOp strEquals

patternDelim :: SParser Unit
patternDelim = arrow <|> equals

-- "curried" controls whether nested functions are permitted in this context
branch :: Boolean -> SParser (Expr ğ”¹) -> SParser Unit -> SParser (Branch ğ”¹)
branch curried expr' delim = do
   Ï€s <- if curried
         then some $ simplePattern pattern
         else NonEmptyList <$> pattern `lift2 (:|)` pure Nil
   e <- delim *> expr'
   pure $ Ï€s Ã— e

branches :: Boolean -> SParser (Expr ğ”¹) -> SParser (NonEmptyList (Branch ğ”¹))
branches curried expr' =
   pure <$> branch curried expr' patternDelim <|> branchMany
   where
   branchMany :: SParser (NonEmptyList (Branch ğ”¹))
   branchMany = token.braces $ sepBy1 (branch curried expr' arrow) token.semi

varDefs :: SParser (Expr ğ”¹) -> SParser (VarDefs ğ”¹)
varDefs expr' = keyword strLet *> sepBy1_try clause token.semi
   where
   clause :: SParser (VarDef ğ”¹)
   clause = (pattern <* patternDelim) `lift2 (Ã—)` expr'

recDefs :: SParser (Expr ğ”¹) -> SParser (RecDefs ğ”¹)
recDefs expr' = do
   keyword strLet *> sepBy1_try clause token.semi
   where
   clause :: SParser (Clause ğ”¹)
   clause = ident `lift2 (Ã—)` (branch true expr' equals)

defs :: SParser (Expr ğ”¹) -> SParser (List (VarDefs ğ”¹ + RecDefs ğ”¹))
defs expr' = singleton <$> choose (try $ varDefs expr') (recDefs expr')

-- Tree whose branches are binary primitives and whose leaves are application chains.
expr_ :: SParser (Expr ğ”¹)
expr_ = fix $ appChain >>> buildExprParser (operators binaryOp)
   where
   -- Syntactically distinguishing infix constructors from other operators (a la Haskell) allows us to
   -- optimise an application tree into a (potentially partial) constructor application.
   binaryOp :: String -> SParser (Expr ğ”¹ -> Expr ğ”¹ -> Expr ğ”¹)
   binaryOp op = do
      op' <- token.operator
      onlyIf (op == op') $
         if isCtrOp op'
         then \e e' -> expr $ Constr (Ctr op') (e : e' : empty)
         else \e e' -> expr $ BinaryApp e op e'

   -- Left-associative tree of applications of one or more simple terms.
   appChain :: Endo (SParser (Expr ğ”¹))
   appChain expr' = simpleExpr >>= rest
      where
      rest :: Expr ğ”¹ -> SParser (Expr ğ”¹)
      rest e@(Expr _ (Constr c es)) = ctrArgs <|> pure e
         where
         ctrArgs :: SParser (Expr ğ”¹)
         ctrArgs = simpleExpr >>= \e' -> rest (expr $ Constr c (es <> (e' : empty)))
      rest e = (expr <$> (App e <$> simpleExpr) >>= rest) <|> pure e

      -- Any expression other than an operator tree or an application chain.
      simpleExpr :: SParser (Expr ğ”¹)
      simpleExpr =
         listEmpty <|>
         listNonEmpty <|>
         try constr <|>
         try variable <|>
         try float <|>
         try int <|> -- int may start with +/-
         string <|>
         defsExpr <|>
         matchAs <|>
         try (token.parens expr') <|>
         try parensOp <|>
         pair <|>
         lambda

         where
         listEmpty :: SParser (Expr ğ”¹)
         listEmpty = token.brackets $ pure $ expr ListEmpty

         listNonEmpty :: SParser (Expr ğ”¹)
         listNonEmpty =
            token.symbol "[" *> (expr <$> (ListNonEmpty <$> expr' <*> fix listRest))

            where
            listRest :: Endo (SParser (ListRest ğ”¹))
            listRest listRest' =
               token.symbol "]" *> pure End <|>
               token.comma *> (Next <$> expr' <*> listRest')

         constr :: SParser (Expr ğ”¹)
         constr = expr <$> (Constr <$> ctr <@> empty)

         variable :: SParser (Expr ğ”¹)
         variable = ident <#> Var >>> expr

         signOpt :: âˆ€ a . Ring a => SParser (a -> a)
         signOpt = (char '-' $> negate) <|> (char '+' $> identity) <|> pure identity

         -- built-in integer/float parsers don't seem to allow leading signs.
         int :: SParser (Expr ğ”¹)
         int = do
            sign <- signOpt
            (sign >>> Int >>> expr) <$> token.natural

         float :: SParser (Expr ğ”¹)
         float = do
            sign <- signOpt
            (sign >>> Float >>> expr) <$> token.float

         string :: SParser (Expr ğ”¹)
         string = (Str >>> expr) <$> token.stringLiteral

         defsExpr :: SParser (Expr ğ”¹)
         defsExpr = do
            defs' <- concat <<< toList <$> sepBy1 (defs expr') token.semi
            foldr (\def -> expr <<< (Let ||| LetRec) def) <$> (keyword strIn *> expr') <@> defs'

         matchAs :: SParser (Expr ğ”¹)
         matchAs =  expr <$> (MatchAs <$> (keyword strMatch *> expr' <* keyword strAs) <*> branches false expr')

         -- any binary operator, in parentheses
         parensOp :: SParser (Expr ğ”¹)
         parensOp = expr <$> (Op <$> token.parens token.operator)

         pair :: SParser (Expr ğ”¹)
         pair = token.parens $
            expr <$> (lift2 $ \e e' -> Constr cPair (e : e' : empty)) (expr' <* token.comma) expr'

         lambda :: SParser (Expr ğ”¹)
         lambda = expr <$> (Lambda <$> (keyword strFun *> branches true expr'))

-- each element of the top-level list corresponds to a precedence level
operators :: forall a . (String -> SParser (a -> a -> a)) -> OperatorTable Identity String a
operators binaryOp =
   fromFoldable $ fromFoldable <$>
   (map (\({ op, assoc }) -> Infix (try $ binaryOp op) assoc)) <$>
   groupBy (eq `on` _.prec) (sortBy (\x -> comparing _.prec x >>> invert) $ values opDefs)

-- Pattern with no continuation.
pattern :: SParser Pattern
pattern = fix $ appChain_pattern >>> buildExprParser (operators infixCtr)
   where
   -- Analogous in some way to app_chain, but nothing higher-order here: no explicit application nodes,
   -- non-saturated constructor applications, or patterns other than constructors in the function position.
   appChain_pattern :: Endo (SParser Pattern)
   appChain_pattern pattern' = simplePattern pattern' >>= rest
      where
         rest âˆ· Pattern -> SParser Pattern
         rest Ï€@(PConstr c Ï€s) = ctrArgs <|> pure Ï€
            where
            ctrArgs :: SParser Pattern
            ctrArgs = simplePattern pattern' >>= \Ï€' -> rest $ PConstr c (Ï€s `snoc` Ï€')
         rest Ï€ = pure Ï€

   infixCtr :: String -> SParser (Pattern -> Pattern -> Pattern)
   infixCtr op = do
      op' <- token.operator
      onlyIf (isCtrOp op' && op == op') \Ï€ Ï€' -> PConstr (Ctr op') (Ï€ : Ï€' : Nil)

topLevel :: forall a . Endo (SParser a)
topLevel p = token.whiteSpace *> p <* eof

program âˆ· SParser (Expr ğ”¹)
program = topLevel expr_

module_ :: SParser (Module ğ”¹)
module_ = Module <<< concat <$> topLevel (sepBy_try (defs expr_) token.semi <* token.semi)
